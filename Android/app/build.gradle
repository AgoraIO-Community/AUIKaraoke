apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "io.agora.karaoke.uikit"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 4
        versionName "0.4.0"
        ndk.abiFilters 'armeabi-v7a'// 'arm64-v8a', 'x86', 'x86-64'

        def properties = new Properties()
        properties.load(rootProject.file("local.properties").newDataInputStream())

        final def SERVER_HOST = properties.getProperty("SERVER_HOST")
        if (SERVER_HOST.isEmpty()) {
            throw new GradleException("请在 local.properties 中配置完整的 SERVER_HOST")
        }

        buildConfigField "String", "SERVER_HOST", "\"$SERVER_HOST\""
    }

    signingConfigs {
        release {
            storeFile new File(rootProject.rootDir.absolutePath + "/keystore.jks")
            storePassword "230511"
            keyAlias "uikit"
            keyPassword "230511"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
    }
    sourceSets {
        main {
            res.srcDirs += "src/main/java/io/agora/app/karaoke/kit/res"
        }
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all { output ->
                outputFileName = new File(rootProject.name + "_" +
                        "v" + defaultConfig.versionName + "_" +
                        new Date().format("yyyyMMddHHmm") + "_" +
                        buildType.name + ".apk")
            }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.github.bumptech.glide:glide:4.15.0'
    implementation 'com.tencent.bugly:crashreport:latest.release'
    implementation project(':asceneskit')
}